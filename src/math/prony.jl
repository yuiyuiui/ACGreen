#
# Note:
#
# The following codes for the Prony approximation are mostly adapted from
#
#     https://github.com/Green-Phys/PronyAC
#
# See
#
#     Minimal Pole Representation and Controlled Analytic Continuation
#     of Matsubara Response Functions
#     Lei Zhang and Emanuel Gull
#     arXiv:2312.10576 (2024)
#
# for more details.
#

#=
### *Customized Structs* : *PronyApproximation*
=#

#=
*Remarks* :

**Prony interpolation**

Our input data consists of an odd number ``2N + 1`` of Matsubara points
``G(i\omega_n)`` that are uniformly spaced. Prony's interpolation method
interpolates ``G_k`` as a sum of exponentials

```math
G_k = \sum^{N-1}_{i=0} w_i \gamma^k_i,
```

where ``0 \le k \le 2N``, ``w_i`` denote complex weights and ``\gamma_i``
corresponding nodes.

---

**Prony approximation**

Prony's interpolation method is unstable. We therefore employs a Prony
approximation, rather than an interpolation of ``G``. For the physical
Matsubara functions, which decay in magnitude to zero for
``i\omega_n \to i\infty``, only ``K \propto \log{1/\varepsilon}`` out of
all ``N`` nodes in the Prony approximation have weights
``|w_i| > \varepsilon``. Thus, we have

```math
\left|G_k - \sum^{K-1}_{i=0} w_i \gamma^k_i\right| \le \varepsilon,
```

for all ``0 \le k \le 2N``.
=#

"""
    PronyApproximation

Mutable struct. Prony approximation to a complex-valued Matsubara function.

### Members
* ğ‘â‚š -> Number of nodes for Prony approximation.
* Ï‰â‚š -> Non-negative Matsubara frequency.
* ğºâ‚š -> Complex values at Ï‰â‚š.
* Î“â‚š -> Nodes for Prony approximation, ``Î³_i``.
* Î©â‚š -> Weights for Prony approximation, ``w_i``.
"""
mutable struct PronyApproximation{T<:Real,S<:Int} <: Function
    ğ‘â‚š::S
    Ï‰â‚š::Vector{T}
    ğºâ‚š::Vector{Complex{T}}
    Î“â‚š::Vector{Complex{T}}
    Î©â‚š::Vector{Complex{T}}
end

"""
    (ğ‘::PronyApproximation)(w::Vector{T}) where {T}

Evaluate the Prony approximation at `w`.

### Arguments
* w -> w âˆˆ â„.

### Returns
* val -> ğ‘.(w).
"""
function (ğ‘::PronyApproximation)(w::Vector{T}) where {T}
    xâ‚€ = @. (w - w[1]) / (w[end] - w[1])
    ğ”¸ = zeros(Complex{T}, length(xâ‚€), length(ğ‘.Î©â‚š))
    #
    for i in eachindex(xâ‚€)
        @. ğ”¸[i, :] = ğ‘.Î“â‚š ^ (2 * ğ‘.ğ‘â‚š * xâ‚€[i])
    end
    #
    return ğ”¸ * ğ‘.Î©â‚š
end

"""
    PronyApproximation(
        ğ‘â‚š :: Int,
        Ï‰â‚š :: Vector{T},
        ğºâ‚š :: Vector{Complex{T}},
        v  :: Vector{Complex{T}}
    )

Construct a `PronyApproximation` type interpolant function. Once it is
available, then it can be used to produce a smooth G at given Ï‰.

This function should not be called directly by the users.

### Arguments
* ğ‘â‚š -> Number of nodes for Prony approximation.
* Ï‰â‚š -> Non-negative Matsubara frequency (postprocessed).
* ğºâ‚š -> Complex values at Ï‰â‚š (postprocessed).
* v  -> Selected vector from the orthogonal matrix `V`.

### Returns
* pa -> A PronyApproximation struct.
"""
function PronyApproximation(ğ‘â‚š::Int, Ï‰â‚š::Vector{T}, ğºâ‚š::Vector{Complex{T}},
                            v::Vector{Complex{T}}) where {T}
    # Evaluate cutoff for Î“â‚š
    Î› = 1+T(1//2) / ğ‘â‚š

    # Evaluate Î“â‚š and Î©â‚š
    Î“â‚š = prony_gamma(v, Î›)
    Î©â‚š = prony_omega(ğºâ‚š, Î“â‚š)

    # Sort Î“â‚š and Î©â‚š
    idx_sort = sortperm(abs.(Î©â‚š))
    reverse!(idx_sort)
    Î©â‚š = Î©â‚š[idx_sort]
    Î“â‚š = Î“â‚š[idx_sort]

    # Return a PronyApproximation struct
    return PronyApproximation(ğ‘â‚š, Ï‰â‚š, ğºâ‚š, Î“â‚š, Î©â‚š)
end

"""
    PronyApproximation(Ï‰â‚::Vector{T}, ğºâ‚::Vector{Complex{T}}, Îµ) where {T}

Construct a `PronyApproximation` type interpolant function. Once it is
available, then it can be used to produce a smooth G at Ï‰.

If the noise level of the input data is known, this function is a good
choice. The parameter `Îµ` can be set to the noise level.

### Arguments
* Ï‰â‚ -> Non-negative Matsubara frequency (raw values).
* ğºâ‚ -> Complex values at Ï‰â‚š (raw values).
* Îµ  -> Threshold for the Prony approximation.

### Returns
* pa -> A PronyApproximation struct.
"""
function PronyApproximation(Ï‰â‚::Vector{T}, ğºâ‚::Vector{Complex{T}}, Îµ) where {T}
    # Preprocess the input data to get the number of nodes, frequency
    # points Ï‰â‚š, and Matsubara data ğºâ‚š.
    ğ‘â‚š, Ï‰â‚š, ğºâ‚š = prony_data(Ï‰â‚, ğºâ‚)

    # Perform singular value decomposition and select reasonable `v`.
    S, V = prony_svd(ğ‘â‚š, ğºâ‚š)
    v = prony_v(V, prony_idx(S, Îµ))

    return PronyApproximation(ğ‘â‚š, Ï‰â‚š, ğºâ‚š, v)
end

"""
    PronyApproximation(Ï‰â‚::Vector{T}, ğºâ‚::Vector{Complex{T}}) where {T}

Construct a `PronyApproximation` type interpolant function. Once it is
available, then it can be used to produce a smooth G at Ï‰. Note that this
function employs a smart and iterative algorithm to determine the optimal
Prony approximation.

This function is time-consuming. But if the noise level of the input data
is unknown, this function is useful.

### Arguments
* Ï‰â‚ -> Non-negative Matsubara frequency (raw values).
* ğºâ‚ -> Complex values at Ï‰â‚š (raw values).

### Returns
* pa -> A PronyApproximation struct.
"""
function PronyApproximation(Ï‰â‚::Vector{T}, ğºâ‚::Vector{Complex{T}}) where {T}
    # Preprocess the input data to get the number of nodes, frequency
    # points Ï‰â‚š, and Matsubara data ğºâ‚š.
    ğ‘â‚š, Ï‰â‚š, ğºâ‚š = prony_data(Ï‰â‚, ğºâ‚)

    # Perform singular value decomposition
    S, V = prony_svd(ğ‘â‚š, ğºâ‚š)

    # Next we should determine the optimal `v`
    #
    # (1) Find maximum index for the exponentially decaying region.
    exp_idx = prony_idx(S)
    #
    # (2) Find minimum index
    Îµ = 1000 * S[exp_idx]
    new_idx = findfirst(x -> x < Îµ, S)
    #
    # (3) Create lists for chosen indices and the corresponding errors.
    idxrange = range(new_idx, min(exp_idx + 10, length(S)))
    idx_list = collect(idxrange)
    err_list = zeros(T, length(idx_list))
    #
    # (4) Create a list of pseudo-PronyApproximations, and then evaluate
    # their reliabilities and accuracies.
    for i in eachindex(idx_list)
        idx = idx_list[i]
        #
        # Extract `v`
        v = prony_v(V, idx)
        #
        # Reproduce G using the pseudo PronyApproximation
        ğºâ‚™ = PronyApproximation(ğ‘â‚š, Ï‰â‚š, ğºâ‚š, v)(Ï‰â‚š)
        #
        # Evaluate the difference and record it
        err_ave = mean(abs.(ğºâ‚™ - ğºâ‚š))
        err_list[i] = err_ave
    end
    #
    # (5) Find the optimal `v`, which should minimize |ğºâ‚™ - ğºâ‚š|
    idx = idx_list[argmin(err_list)]
    v = prony_v(V, idx)
    println("The optimal Prony approximation is $idx")

    return PronyApproximation(ğ‘â‚š, Ï‰â‚š, ğºâ‚š, v)
end

"""
    prony_data(Ï‰â‚::Vector{T}, ğºâ‚::Vector{Complex{T}}) where {T}

Prepare essential data for the later Prony approximation. It will return
the number of nodes ğ‘â‚š, frequency mesh Ï‰â‚š, and Green's function data ğºâ‚š
at this mesh.

### Arguments
* Ï‰â‚ -> Non-negative Matsubara frequency (raw values).
* ğºâ‚ -> Complex values at Ï‰â‚š (raw values), ğºâ‚ = G(Ï‰â‚).

### Returns
See above explanations.
"""
function prony_data(Ï‰â‚::Vector{T}, ğºâ‚::Vector{Complex{T}}) where {T}
    # We have to make sure the number of data points is odd.
    osize = length(Ï‰â‚)
    nsize = iseven(osize) ? osize - 1 : osize
    #
    ğ‘â‚š = div(nsize, 2) # Number of nodes for Prony approximation
    Ï‰â‚š = Ï‰â‚[1:nsize]   # Matsubara frequency, Ï‰â‚™
    ğºâ‚š = ğºâ‚[1:nsize]   # Matsubara Green's function, G(iÏ‰â‚™)
    #
    return ğ‘â‚š, Ï‰â‚š, ğºâ‚š
end

"""
    prony_svd(ğ‘â‚š::Int, ğºâ‚š::Vector{T}) where {T}

Perform singular value decomposition for the matrix â„‹ that is constructed
from ğºâ‚š. It will return the singular values `S` and orthogonal matrix `V`.

### Arguments
* ğ‘â‚š -> Number of nodes.
* ğºâ‚š -> Truncated Green's function data.

### Returns
See above explanations.

See also: [`prony_data`](@ref).
"""
function prony_svd(ğ‘â‚š::Int, ğºâ‚š::Vector{T}) where {T}
    â„‹ = zeros(T, ğ‘â‚š + 1, ğ‘â‚š + 1)
    #
    for i in 1:(ğ‘â‚š + 1)
        â„‹[i, :] = ğºâ‚š[i:(i + ğ‘â‚š)]
    end
    #
    _, S, V = LinearAlgebra.svd(â„‹)
    println("Singular values: $S")

    return S, V
end

"""
    prony_idx(S::Vector{T}, Îµ::T) where {T}

The diagonal matrix (singular values) `S` is used to test whether the
threshold `Îµ` is reasonable and figure out the index for extracting `v`
from `V`.

### Arguments
* S -> Singular values of â„‹.
* Îµ -> Threshold provided by the users.

### Returns
* idx -> Index for S[idx] < Îµ.

See also: [`prony_v`](@ref) and [`prony_svd`](@ref).
"""
function prony_idx(S::Vector{T}, Îµ) where {T}
    # Determine idx, such that S[idx] < Îµ.
    idx = findfirst(x -> x < Îµ, S)

    # Check idx
    if isnothing(idx)
        error("Please increase Îµ and try again! Îµ âˆˆ [$(S[1]),$(S[end])]")
    end

    return idx
end

"""
    prony_idx(S::Vector{T}) where {T}

The diagonal matrix (singular values) `S` is used to figure out the index
for extracting `v` from `V`. This function is try to evaluate the maximum
index for the exponentially decaying region of `S`.

### Arguments
* S -> Singular values of â„‹.

### Returns
* idx -> Index for extracting `v` from `V`.

See also: [`prony_v`](@ref).
"""
function prony_idx(S::Vector{T}) where {T}
    n_max = min(3 * floor(Int, log(1.0e12)), floor(Int, 0.8 * length(S)))
    #
    idx_fit = collect(range(ceil(Int, 0.8*n_max), n_max))
    val_fit = S[idx_fit]
    ğ”¸ = hcat(idx_fit, ones(Int, length(idx_fit)))
    #
    ğ‘, ğ‘ = pinv(ğ”¸) * log.(val_fit)
    ğ•Š = exp.(ğ‘ .* collect(range(1, n_max)) .+ ğ‘)
    #
    idx = count(S[1:n_max] .> 5 * ğ•Š) + 1

    return idx
end

"""
    prony_v(V::Adjoint{T, Matrix{T}}, idx::Int) where {T}

Extract suitable vector `v` from orthogonal matrix `V` according to the
threshold `Îµ`.

### Arguments
* V -> Orthogonal matrix from singular value decomposition of â„‹.
* idx -> Index for extracting `v` from `V`.

### Returns
* v -> Vector `v` extracted from `V` according to `idx`.

See also: [`prony_svd`](@ref).
"""
function prony_v(V::Adjoint{T,Matrix{T}}, idx::Int) where {T}
    # Extract v from V
    println("Selected vector from orthogonal matrix V: ", idx)
    v = V[:, idx]

    return reverse!(v)
end

"""
    prony_gamma(v::Vector{Complex{T}}, Î›::T) where {T}

Try to calculate Î“â‚š. Actually, Î“â‚š are eigenvalues of a matrix constructed
by `v`. `Î›` is a cutoff for Î“â‚š. Only those Î“â‚š that are smaller than `Î›`
are kept.

### Arguments
* v -> A vector extracted from `V`.
* Î› -> A cutoff for Î“â‚š.

### Returns
* Î“â‚š -> Roots of a polynominal with coefficients given in `v`.

See also: [`prony_v`](@ref).
"""
function prony_gamma(v::Vector{Complex{T}}, Î›::T) where {T}
    # The following codes actually calculate the roots of a polynominal
    # with coefficients given in v. The roots are Î“â‚š.
    non_zero = findall(!iszero, v)
    trailing_zeros = length(v) - non_zero[end]
    #
    vnew = v[non_zero[1]:non_zero[end]]
    N = length(vnew)
    #
    if N > 1
        A = diagm(-1=>ones(Complex{T}, N - 2))
        @. A[1, :] = -vnew[2:end] / vnew[1]
        roots = eigvals(A)
    else
        roots = []
    end
    #
    Î“â‚š = vcat(roots, zeros(Complex{T}, trailing_zeros))

    filter!(x -> abs(x) < Î›, Î“â‚š)

    return Î“â‚š
end

"""
    prony_omega(ğºâ‚š::Vector{T}, Î“â‚š::Vector{T}) where {T}

Try to calculate Î©â‚š.

### Arguments
* ğºâ‚š -> Complex values at Ï‰â‚š.
* Î“â‚š -> Nodes for Prony approximation, ``Î³_i``.

### Returns
* Î©â‚š -> Weights for Prony approximation, ``w_i``.
"""
function prony_omega(ğºâ‚š::Vector{T}, Î“â‚š::Vector{T}) where {T}
    A = zeros(T, length(ğºâ‚š), length(Î“â‚š))
    #
    for i in eachindex(ğºâ‚š)
        A[i, :] = Î“â‚š .^ (i - 1)
    end
    #
    return pinv(A) * ğºâ‚š
end
