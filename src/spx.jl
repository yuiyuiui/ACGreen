#
# Project : Gardenia
# Source  : spx.jl
# Author  : Li Huang (huangli@caep.cn)
# Status  : Unstable
#
# Last modified: 2024/09/30
#

#=
### *Customized Structs* : *StochPX Solver*
=#

"""
    StochPXMC

Mutable struct. It is used within the StochPX solver. It includes random
number generator and some counters.

### Members
* rng  -> Random number generator.
* Sacc -> Counter for position-updated (type 1) operation (accepted).
* Stry -> Counter for position-updated (type 1) operation (tried).
* Pacc -> Counter for position-updated (type 2) operation (accepted).
* Ptry -> Counter for position-updated (type 2) operation (tried).
* Aacc -> Counter for amplitude-updated operation (accepted).
* Atry -> Counter for amplitude-updated operation (tried).
* Xacc -> Counter for exchange operation (accepted).
* Xtry -> Counter for exchange operation (tried).

See also: [`StochPXSolver`](@ref).
"""
mutable struct StochPXMC{I<:Int}
    rng::AbstractRNG
    Sacc::I
    Stry::I
    Pacc::I
    Ptry::I
    Aacc::I
    Atry::I
    Xacc::I
    Xtry::I
end

"""
    StochPXElement

Mutable struct. It is used to record the field configurations, which will
be sampled by Monte Carlo sweeping procedure.

For the off-diagonal elements of the matrix-valued Green's function, the
signs of the poles (ùïä) could be negative (-1.0). However, for the other
cases, ùïä is always positive (+1.0).

### Members
* P  -> It means the positions of the poles.
* A  -> It means the weights / amplitudes of the poles.
* ùïä  -> It means the signs of the poles.
"""
mutable struct StochPXElement{I<:Int,T<:Real}
    P::Vector{I}
    A::Vector{T}
    ùïä::Vector{T}
end

"""
    StochPXContext

Mutable struct. It is used within the StochPX solver only.

Note that œá¬≤ denotes the goodness-of-fit functional, and G·µß denotes the
reproduced correlator. They should be always compatible with P, A, and ùïä
in the StochPXElement struct.

### Members
* G·µ•    -> Input data for correlator.
* G·µß    -> Generated correlator.
* œÉ¬π    -> Actually 1.0 / œÉ¬π.
* allow -> Allowable indices.
* wn  -> Grid for input data.
* mesh  -> Mesh for output spectrum.
* fmesh -> Very dense mesh for the poles.
* Œõ     -> Precomputed kernel matrix.
* Œò     -> Artificial inverse temperature.
* œá¬≤    -> Goodness-of-fit functional for the current configuration.
* œá¬≤·µ•   -> Vector of goodness-of-fit functional.
* P·µ•    -> Vector of poles' positions.
* A·µ•    -> Vector of poles' amplitudes.
* ùïä·µ•    -> Vector of poles' signs.
"""
mutable struct StochPXContext{I<:Int,T<:Real}
    G·µ•::Vector{T}
    G·µß::Vector{T}
    œÉ¬π::T
    allow::Vector{I}
    wn::Vector{T}
    mesh::Vector{T}
    mesh_weight::Vector{T}
    fmesh::Vector{T}
    Œõ::Array{T,2}
    Œò::T
    œá¬≤::T
    œá¬≤·µ•::Vector{T}
    P·µ•::Vector{Vector{I}}
    A·µ•::Vector{Vector{T}}
    ùïä·µ•::Vector{Vector{T}}
end

#=
### *Global Drivers*
=#

"""
    solve(S::StochPXSolver, rd::RawData)

Solve the analytic continuation problem by the stochastic pole expansion.
It is the driver for the StochPX solver. Note that this solver is still
`experimental`. It is useful for analytic continuation of Matsubara data.
In other words, this solver should not be used to handle the imagnary-time
Green's function.

Similar to the BarRat and NevanAC solvers, this solver always returns A(œâ).

### Arguments
* S -> A StochPXSolver struct.
* rd -> A RawData struct, containing raw data for input correlator.

### Returns
* mesh -> Real frequency mesh, œâ.
* Aout -> Spectral function, A(œâ).
* Gout -> Retarded Green's function, G(œâ).
"""
function solve(GFV::Vector{Complex{T}}, ctx::CtxData, alg::SPX) where {T<:Real}
    println("[ StochPX ]")
    ctx.spt isa Cont && (alg.method == "best") &&
        error("SPX with method = \"mean\" is recommended for cont type spectrum")
    ctx.spt isa Delta && (alg.method == "mean") &&
        error("SPX with method = \"best\" is recommended for delta type spectrum")
    fine_mesh = collect(range(ctx.mesh[1], ctx.mesh[end], alg.nfine)) # spx needs high-precise linear grid
    MC = init_mc(alg)
    println("Create infrastructure for Monte Carlo sampling")

    # Initialize Monte Carlo configurations
    SE = init_element(alg, MC.rng, T)
    println("Randomize Monte Carlo configurations")

    # Prepare some key variables
    G·µ• = vcat(real(GFV), (imag(GFV)))
    G·µß, Œõ, Œò, œá¬≤, œá¬≤·µ•, P·µ•, A·µ•, ùïä·µ• = init_context(alg, SE, ctx.wn, fine_mesh, G·µ•)
    SC = StochPXContext(G·µ•, G·µß, T(1/ctx.œÉ), collect(1:alg.nfine), ctx.wn, ctx.mesh,
                        ctx.mesh_weight, fine_mesh,
                        Œõ, Œò, œá¬≤, œá¬≤·µ•, P·µ•, A·µ•, ùïä·µ•)
    println("Initialize context for the StochPX solver")

    Aout, _, _ = run!(MC, SE, SC, alg)
    if ctx.spt isa Cont
        return SC.mesh, Aout
    elseif ctx.spt isa Delta
        p = ctx.mesh[find_peaks(ctx.mesh, Aout, ctx.fp_mp; wind=ctx.fp_ww)]
        Œ≥ = ones(T, length(p)) ./ length(p)
        return SC.mesh, Aout, (p, Œ≥)
    else
        error("Unsupported spectral function type")
    end
end

"""
    run!(MC::StochPXMC, SE::StochPXElement, SC::StochPXContext, alg::SPX)

Perform stochastic pole expansion simulation, sequential version.

### Arguments
* MC -> A StochPXMC struct.
* SE -> A StochPXElement struct.
* SC -> A StochPXContext struct.
* alg -> A SPX struct.

### Returns
* Aout -> Spectral function, A(œâ).
* Gout -> Retarded Green's function, G(œâ).
* G·µ£ -> Reproduced Green's function, G(iœâ‚Çô).
"""
function run!(MC::StochPXMC{I}, SE::StochPXElement{I,T}, SC::StochPXContext{I,T},
              alg::SPX) where {I<:Int,T<:Real}

    # Setup essential parameters
    ntry = alg.ntry
    nstep = alg.nstep

    # Warmup the Monte Carlo engine
    println("Start thermalization...")
    for _ in 1:nstep
        sample!(1, MC, SE, SC, alg)
    end

    # Sample and collect data
    println("Start stochastic sampling...")
    for t in 1:ntry
        # Reset Monte Carlo counters
        reset_mc!(MC)

        # Reset Monte Carlo field configuration
        reset_element!(MC.rng, SE, alg)

        # Reset G·µß and œá¬≤ in SC (StochPXContext)
        reset_context!(t, SE, SC, alg)

        # Apply simulated annealing algorithm
        for _ in 1:nstep
            sample!(t, MC, SE, SC, alg)
        end

        # Show the best œá¬≤ (the smallest) for the current attempt
        t%10 == 0 && println("try = $t -> [œá¬≤ = $(SC.œá¬≤·µ•[t])]")
    end

    # Generate spectral density from Monte Carlo field configuration
    return average!(SC, alg)
end

"""
    average!(SC::StochPXContext, alg::SPX)

Postprocess the results generated during the stochastic pole expansion
simulations. It will calculate the spectral functions, real frequency
Green's function, and imaginary frequency Green's function.

### Arguments
* SC -> A StochPXContext struct.
* alg -> A SPX struct.

### Returns
* Aout -> Spectral function, A(œâ).
* Gout -> Retarded Green's function, G(œâ).
* G·µ£ -> Reproduced Green's function, G(iœâ‚Çô).
"""
function average!(SC::StochPXContext{I,T}, alg::SPX) where {I<:Int,T<:Real}
    # Setup essential parameters
    nmesh = length(SC.mesh)
    method = alg.method
    ntry = alg.ntry

    # Allocate memory
    # Gout: real frequency Green's function, G(œâ).
    # G·µ£: imaginary frequency Green's function, G(iœâ‚Çô)
    ngrid, _ = size(SC.Œõ)
    Gout = zeros(Complex{T}, nmesh)
    G·µ£ = zeros(T, ngrid)

    # Choose the best solution
    if method == "best"
        # The œá¬≤ of the best solution should be the smallest.
        p = argmin(SC.œá¬≤·µ•)
        println("Best solution: try = $p -> [œá¬≤ = $(SC.œá¬≤·µ•[p])]")
        #
        # Calculate G(œâ)
        Gout = calc_green(p, SC, true, alg)
        #
        # Calculate G(iœâ‚Çô)
        G·µ£ = calc_green(p, SC, false, alg)
        #
        # Collect the `good` solutions and calculate their average.
    else
        # Calculate the median of SC.œá¬≤·µ•
        chi2_med = median(SC.œá¬≤·µ•)
        chi2_ave = mean(SC.œá¬≤·µ•)

        # Determine the Œ±good parameter, which is used to filter the
        # calculated spectra.
        Œ±good = T(1.2)
        if count(x -> x < chi2_med / Œ±good, SC.œá¬≤·µ•) ‚â§ ntry / 10
            Œ±good = T(1)
        end

        # Go through all the solutions
        c = T(0) # A counter
        passed = I[]
        for i in 1:ntry
            if SC.œá¬≤·µ•[i] < chi2_med / Œ±good
                # Calculate and accumulate G(œâ)
                G = calc_green(i, SC, true, alg)
                @. Gout = Gout + G
                #
                # Calculate and accumulate G(iœâ‚Çô)
                G = calc_green(i, SC, false, alg)
                @. G·µ£ = G·µ£ + G
                #
                # Increase the counter
                c = c + T(1)
                append!(passed, i)
            end
        end
        #
        # Normalize the final results
        @. Gout = Gout / c
        @. G·µ£ = G·µ£ / c
        println("Mean value of œá¬≤: $(chi2_ave)")
        println("Median value of œá¬≤: $(chi2_med)")
        println("Accumulate $(round(Int,c)) solutions to get the spectral density")
    end

    return -imag.(Gout) / œÄ, Gout, G·µ£
end

#=
### *Core Algorithms*
=#

"""
    sample!(
        t::I,
        MC::StochPXMC{I},
        SE::StochPXElement{I, T},
        SC::StochPXContext{I, T},
        alg::SPX
    ) where {I<:Int, T<:Real}

Try to search the configuration space to locate the minimum by using the
simulated annealing algorithm. Here, `t` means the t-th attempt.

### Arguments
* t -> Counter for attempts.
* MC -> A StochPXMC struct.
* SE -> A StochPXElement struct.
* SC -> A StochPXContext struct.

### Returns
N/A
"""
function sample!(t::I,
                 MC::StochPXMC{I},
                 SE::StochPXElement{I,T},
                 SC::StochPXContext{I,T},
                 alg::SPX) where {I<:Int,T<:Real}
    # Try to change positions of poles
    if rand(MC.rng) < 0.5
        if rand(MC.rng) < 0.9
            try_move_s!(t, MC, SE, SC, alg)
        else
            try_move_p!(t, MC, SE, SC, alg)
        end
        # Try to change amplitudes of poles
    else
        if rand(MC.rng) < 0.5
            try_move_a!(t, MC, SE, SC, alg)
        else
            try_move_x!(t, MC, SE, SC, alg)
        end
    end
end

"""
    measure!(t::I, SE::StochPXElement{I, T}, SC::StochPXContext{I, T}) where {I<:Int, T<:Real}

Store Monte Carlo field configurations (positions, amplitudes, and signs
of many poles) for the `t`-th attempt. In other words, the current field
configuration (recorded in `SE`) will be saved in `SC`.

Note that not all configurations for the `t`-th attempt will be saved.
Only the solution that exhibits the smallest œá¬≤ will be saved. For the
`t`-th attempt, the StochPX solver will do `nstep` Monte Carlo updates.
It will calculate all œá¬≤, and try to figure out the smallest one. Then
the corresponding configuration (solution) will be saved.

### Arguments
* t -> Counter for the attemps.
* SE -> A StochPXElement struct.
* SC -> A StochPXContext struct.

### Returns
N/A
"""
function measure!(t::I, SE::StochPXElement{I,T},
                  SC::StochPXContext{I,T}) where {I<:Int,T<:Real}
    SC.œá¬≤·µ•[t] = SC.œá¬≤
    #
    @. SC.P·µ•[t] = SE.P
    @. SC.A·µ•[t] = SE.A
    @. SC.ùïä·µ•[t] = SE.ùïä
end

#=
### *Service Functions*
=#

"""
    init_mc(alg::SPX)

Try to create a StochPXMC struct. Some counters for Monte Carlo updates
are initialized here.

### Arguments
* S -> A StochPXSolver struct.

### Returns
* MC -> A StochPXMC struct.

See also: [`StochPXMC`](@ref).
"""
function init_mc(alg::SPX)
    seed = rand(1:100000000)
    rng = MersenneTwister(seed)
    #
    Sacc = 0
    Stry = 0
    #
    Pacc = 0
    Ptry = 0
    #
    Aacc = 0
    Atry = 0
    #
    Xacc = 0
    Xtry = 0
    #
    MC = StochPXMC(rng, Sacc, Stry, Pacc, Ptry, Aacc, Atry, Xacc, Xtry)

    return MC
end

"""
    init_element(
        alg::SPX,
        rng::AbstractRNG,
        T::Type{<:Real}
    ) where {T<:Real}

Randomize the configurations for future Monte Carlo sampling. It will
return a StochPXElement struct.

### Arguments
* alg -> A SPX struct.
* rng -> Random number generator.
* T -> Type of the real number.
### Returns
* SE -> A StochPXElement struct.

See also: [`StochPXElement`](@ref).
"""
function init_element(alg::SPX,
                      rng::AbstractRNG,
                      T::Type{<:Real})
    npole = alg.npole
    # Initialize P, A, and ùïä
    P = rand(rng, collect(1:alg.nfine), npole)
    A = rand(rng, T, npole)
    ùïä = ones(T, npole)

    # We have to make sure ‚àë·µ¢ A·µ¢ = 1
    s = sum(A)
    @. A = A / s

    SE = StochPXElement(abs.(P), A, ùïä)

    return SE
end

"""
    init_context(
        alg::SPX,
        SE::StochPXElement{I, T},
        wn::Vector{T},
        fmesh::Vector{T},
        G·µ•::Vector{T}
    ) where {I<:Int, T<:Real}

Try to initialize the key members of a StochPXContext struct. It will try
to return some key variables, which should be used to construct the
StochPXContext struct.

### Arguments
* alg -> A SPX struct.
* SE    -> A StochPXElement struct.
* wn    -> Grid for input correlator.
* fmesh -> Fine mesh in [wmin, wmax], used to build the kernel matrix Œõ.
* G·µ•    -> Preprocessed input correlator.

### Returns
* G·µß  -> Reconstructed correlator.
* Œõ   -> Precomputed kernel matrix.
* Œò   -> Artificial inverse temperature.
* œá¬≤  -> Current goodness-of-fit functional.
* œá¬≤·µ• -> Vector of goodness-of-fit functional.
* P·µ•  -> Vector of poles' positions.
* A·µ•  -> Vector of poles' amplitudes.
* ùïä·µ•  -> Vector of poles' signs.

See also: [`StochPXContext`](@ref).
"""
function init_context(alg::SPX,
                      SE::StochPXElement{I,T},
                      wn::Vector{T},
                      fmesh::Vector{T},
                      G·µ•::Vector{T}) where {I<:Int,T<:Real}
    # Extract some parameters
    ntry = alg.ntry
    npole = alg.npole

    # Prepare the kernel matrix Œõ. It is used to speed up the simulation.
    # Note that Œõ depends on the type of kernel.
    Œõ = calc_lambda(wn, fmesh)

    # We have to make sure that the starting G·µß and œá¬≤ are consistent with
    # the current Monte Carlo configuration fields.
    G·µß = calc_green(SE.P, SE.A, SE.ùïä, Œõ)
    œá¬≤ = calc_chi2(G·µß, G·µ•)

    # œá¬≤·µ• is initialized by a large number. Later it will be updated by
    # the smallest œá¬≤ during the simulation.
    œá¬≤·µ• = zeros(T, ntry)
    @. œá¬≤·µ• = T(1e10)

    # P, A, and ùïä should be always compatible with œá¬≤. They are updated
    # in the `measure!()` function.
    P·µ• = Vector{I}[]
    A·µ• = Vector{T}[]
    ùïä·µ• = Vector{T}[]
    #
    for _ in 1:ntry
        push!(P·µ•, ones(I, npole))
        push!(A·µ•, zeros(T, npole))
        push!(ùïä·µ•, zeros(T, npole))
    end

    return G·µß, Œõ, T(alg.theta), œá¬≤, œá¬≤·µ•, P·µ•, A·µ•, ùïä·µ•
end

"""
    reset_mc!(MC::StochPXMC)

Reset the counters in StochPXMC struct.

### Arguments
* MC -> A StochPXMC struct.

### Returns
N/A
"""
function reset_mc!(MC::StochPXMC)
    MC.Sacc = 0
    MC.Stry = 0
    #
    MC.Pacc = 0
    MC.Ptry = 0
    #
    MC.Aacc = 0
    MC.Atry = 0
    #
    MC.Xacc = 0
    return MC.Xtry = 0
end

"""
    reset_element!(
        rng::AbstractRNG,
        SE::StochPXElement,
        alg::SPX
    )

Reset the Monte Carlo field configurations (i.e. positions and amplitudes
of the poles). Note that the signs of the poles should not be changed.
Be careful, the corresponding œá¬≤ (goodness-of-fit functional) and G·µß
(generated correlator) will be updated in the `reset_context!()` function.

### Arguments
* rng   -> Random number generator.
* allow -> Allowed positions for the poles.
* SE -> A StochPXElement struct.

### Returns
N/A
"""
function reset_element!(rng::AbstractRNG,
                        SE::StochPXElement{I,T},
                        alg::SPX) where {I<:Int,T<:Real}
    npole = alg.npole
    allow = collect(1:alg.nfine)

    # How many poles should be changed
    if npole ‚â§ 5
        if 4 ‚â§ npole ‚â§ 5
            nselect = 2
        else
            nselect = 1
        end
    else
        nselect = npole √∑ 5
    end
    @assert nselect ‚â§ npole

    # Which poles should be changed
    selected = rand(rng, 1:npole, nselect)
    unique!(selected)
    nselect = length(selected)

    # Change poles' positions
    if rand(rng) < 0.5
        P = rand(rng, allow, nselect)
        @. SE.P[selected] = P
        # Change poles' amplitudes
    else
        A‚ÇÅ = SE.A[selected]
        s‚ÇÅ = sum(A‚ÇÅ)
        #
        A‚ÇÇ = rand(rng, T, nselect)
        s‚ÇÇ = sum(A‚ÇÇ)
        @. A‚ÇÇ = A‚ÇÇ / s‚ÇÇ * s‚ÇÅ
        #
        @. SE.A[selected] = A‚ÇÇ
    end
end

"""
    reset_context!(t::I, SE::StochPXElement{I, T}, SC::StochPXContext{I, T}, alg::SPX) where {I<:Int, T<:Real}

Recalculate imaginary frequency Green's function G·µß and goodness-of-fit
functional œá¬≤ by new Monte Carlo field configurations for the `t`-th
attempts. They must be consistent with each other.

Some key variables in `SC` are also updated as well. Perhaps we should
develop a smart algorhtm to update Œò here.

### Arguments
* t -> Counter for attempts.
* SE -> A StochPXElement struct.
* SC -> A StochPXContext struct.
* alg -> A SPX struct.

### Returns
N/A
"""
function reset_context!(t::I, SE::StochPXElement{I,T}, SC::StochPXContext{I,T},
                        alg::SPX) where {I<:Int,T<:Real}
    SC.Œò = alg.theta
    SC.G·µß = calc_green(SE.P, SE.A, SE.ùïä, SC.Œõ)
    SC.œá¬≤ = calc_chi2(SC.G·µß, SC.G·µ•)
    return SC.œá¬≤·µ•[t] = T(1e10)
end

"""
    calc_lambda(wn::Vector{T}, fmesh::Vector{T}) where {T<:Real}

Precompute the kernel matrix Œõ (Œõ ‚â° 1 / (iœâ‚Çô - œµ)). It is a service
function and is for the fermionic systems.

### Arguments
* wn    -> Imaginary axis grid for input data.
* fmesh -> Very dense mesh in [wmin, wmax].

### Returns
* Œõ -> The kernel matrix, a 2D array.
"""
function calc_lambda(wn::Vector{T}, fmesh::Vector{T}) where {T<:Real}
    ngrid = length(wn)
    nfine = length(fmesh)

    _Œõ = zeros(Complex{T}, ngrid, nfine)
    #
    for i in eachindex(wn)
        iœâ‚Çô = im * wn[i]
        for j in eachindex(fmesh)
            _Œõ[i, j] = 1 / (iœâ‚Çô - fmesh[j])
        end
    end
    #
    Œõ = vcat(real(_Œõ), imag(_Œõ))

    return Œõ
end

"""
    calc_green(t::I, SC::StochPXContext{I, T}, real_axis::Bool) where {I<:Int, T<:Real}

Reconstruct Green's function at imaginary axis or real axis by using the
pole expansion. It is a driver function. If `real_axis = true`, it will
returns G(œâ), or else G(iœâ‚Çô).

### Arguments
* t -> Index of the current attempt.
* SC -> A StochPXContext struct.
* real_axis -> Working at real axis (true) or imaginary axis (false)?

### Returns
* G -> Reconstructed Green's function, G(œâ) or G(iœâ‚Çô).
"""
function calc_green(t::I, SC::StochPXContext{I,T}, real_axis::Bool,
                    alg::SPX) where {I<:Int,T<:Real}
    @assert t ‚â§ alg.ntry
    # Calculate G(iœâ‚Çô)
    if real_axis == false
        return calc_green(SC.P·µ•[t], SC.A·µ•[t], SC.ùïä·µ•[t], SC.Œõ)
    end
    return calc_green(SC.P·µ•[t], SC.A·µ•[t], SC.ùïä·µ•[t], SC.mesh, SC.fmesh, T(alg.eta))
end

"""
    calc_green(
        P::Vector{I},
        A::Vector{T},
        ùïä::Vector{T},
        Œõ::Array{T,2}
    ) where {I<:Int, T<:Real}

Reconstruct Green's function at imaginary axis by the pole expansion.

### Arguments
* P -> Positions of poles.
* A -> Amplitudes of poles.
* ùïä -> Signs of poles.
* Œõ -> Kernel matrix Œõ.
* alg -> A SPX struct.

### Returns
* G -> Reconstructed Green's function, G(iœâ‚Çô).
"""
function calc_green(P::Vector{I},
                    A::Vector{T},
                    ùïä::Vector{T},
                    Œõ::Array{T,2}) where {I<:Int,T<:Real}
    # Note that here `ngrid` is equal to 2 √ó ngrid sometimes.
    ngrid, _ = size(Œõ)

    G = zeros(T, ngrid)
    for i in 1:ngrid
        G[i] = dot(A .* ùïä, Œõ[i, P])
    end

    return G
end

"""
    calc_green(
        P::Vector{I},
        A::Vector{T},
        ùïä::Vector{T},
        mesh::Vector{T},
        fmesh::Vector{T},
        Œ∑::T
    ) where {I<:Int, T<:Real}

Reconstruct Green's function at real axis by the pole expansion. It is
for the fermionic systems only.

### Arguments
* P     -> Positions of poles.
* A     -> Amplitudes of poles.
* ùïä     -> Signs of poles.
* mesh  -> Real frequency mesh for spectral functions.
* fmesh -> Very dense real frequency mesh for poles.
* Œ∑ -> Imaginary time step.

### Returns
* G -> Retarded Green's function, G(œâ).
"""
function calc_green(P::Vector{I},
                    A::Vector{T},
                    ùïä::Vector{T},
                    mesh::Vector{T},
                    fmesh::Vector{T},
                    Œ∑::T) where {I<:Int,T<:Real}
    nmesh = length(mesh)

    iœâ‚Çô = mesh .+ im * Œ∑
    G = zeros(Complex{T}, nmesh)
    for i in eachindex(mesh)
        G[i] = sum(@. (A * ùïä) / (iœâ‚Çô[i] - fmesh[P]))
    end

    return G
end

"""
    calc_chi2(G‚Çô::Vector{T}, G·µ•::Vector{T}) where {T<:Real}

Try to calculate the goodness-of-fit function (i.e, œá¬≤), which measures
the distance between input and regenerated correlators.

### Arguments
* G‚Çô -> Reconstructed Green's function.
* G·µ• -> Original Green's function.

### Returns
* chi2 -> Goodness-of-fit functional, œá¬≤.

See also: [`calc_green`](@ref).
"""
function calc_chi2(G‚Çô::Vector{T}, G·µ•::Vector{T}) where {T<:Real}
    ŒîG = G‚Çô - G·µ•
    return dot(ŒîG, ŒîG)
end

"""
    try_move_s!(
        t::I,
        MC::StochPXMC{I},
        SE::StochPXElement{I, T},
        SC::StochPXContext{I, T},
        alg::SPX
    ) where {I<:Int, T<:Real}

Change the position of one randomly selected pole.

### Arguments
* t -> Counter of attempts.
* MC -> A StochPXMC struct.
* SE -> A StochPXElement struct.
* SC -> A StochPXContext struct.
* alg -> A SPX struct.

### Returns
N/A

See also: [`try_move_p`](@ref).
"""
function try_move_s!(t::I,
                     MC::StochPXMC{I},
                     SE::StochPXElement{I,T},
                     SC::StochPXContext{I,T},
                     alg::SPX) where {I<:Int,T<:Real}
    # Get parameters
    ngrid = length(SC.G·µ•)
    nfine = alg.nfine
    npole = alg.npole
    move_window = nfine √∑ 100

    # It is used to save the change of Green's function
    Œ¥G = zeros(T, ngrid)
    G‚Çô = zeros(T, ngrid)

    # Try to go through each pole
    for _ in 1:npole

        # Select one pole randomly
        s = rand(MC.rng, 1:npole)

        # Try to change position of the s pole
        A‚Çõ = SE.A[s]
        ùïä‚Çõ = SE.ùïä[s]
        #
        Œ¥P = rand(MC.rng, 1:move_window)
        #
        P‚ÇÅ = SE.P[s]
        P‚ÇÇ = P‚ÇÅ
        if rand(MC.rng) > 0.5
            P‚ÇÇ = P‚ÇÅ + Œ¥P
        else
            P‚ÇÇ = P‚ÇÅ - Œ¥P
        end
        #
        if ùïä‚Çõ > 0.0
            !(+P‚ÇÇ in SC.allow) && continue
        else
            !(-P‚ÇÇ in SC.allow) && continue
        end

        # Calculate change of Green's function
        Œõ‚ÇÅ = view(SC.Œõ, :, P‚ÇÅ)
        Œõ‚ÇÇ = view(SC.Œõ, :, P‚ÇÇ)
        @. Œ¥G = ùïä‚Çõ * A‚Çõ * (Œõ‚ÇÇ - Œõ‚ÇÅ)

        # Calculate new Green's function and goodness-of-fit function
        @. G‚Çô = Œ¥G + SC.G·µß
        œá¬≤ = calc_chi2(G‚Çô, SC.G·µ•)
        Œ¥œá¬≤ = œá¬≤ - SC.œá¬≤

        # Simulated annealing algorithm
        MC.Stry = MC.Stry + 1
        if Œ¥œá¬≤ < 0 || min(1, exp(-Œ¥œá¬≤ * SC.Œò)) > rand(MC.rng)
            # Update Monte Carlo configuration
            SE.P[s] = P‚ÇÇ

            # Update reconstructed Green's function
            @. SC.G·µß = G‚Çô

            # Update goodness-of-fit function
            SC.œá¬≤ = œá¬≤

            # Update Monte Carlo counter
            MC.Sacc = MC.Sacc + 1

            # Save optimal solution
            if SC.œá¬≤ < SC.œá¬≤·µ•[t]
                measure!(t, SE, SC)
            end
        end
    end
end

"""
    try_move_p!(
        t::I,
        MC::StochPXMC{I},
        SE::StochPXElement{I, T},
        SC::StochPXContext{I, T},
        alg::SPX
    ) where {I<:Int, T<:Real}

Change the positions of two randomly selected poles.

### Arguments
* t -> Counter of attempts.
* MC -> A StochPXMC struct.
* SE -> A StochPXElement struct.
* SC -> A StochPXContext struct.
* alg -> A SPX struct.

### Returns
N/A

See also: [`try_move_s!`](@ref).
"""
function try_move_p!(t::I,
                     MC::StochPXMC{I},
                     SE::StochPXElement{I,T},
                     SC::StochPXContext{I,T},
                     alg::SPX) where {I<:Int,T<:Real}
    # Get parameters
    ngrid = length(SC.G·µ•)
    npole = alg.npole

    # Sanity check
    if npole == 1
        return
    end

    # It is used to save the change of Green's function
    Œ¥G = zeros(T, ngrid)
    G‚Çô = zeros(T, ngrid)

    # Try to go through each pole
    for _ in 1:npole

        # Select two poles randomly
        # The two poles should not be the same.
        s‚ÇÅ = 1
        s‚ÇÇ = 1
        while s‚ÇÅ == s‚ÇÇ
            s‚ÇÅ = rand(MC.rng, 1:npole)
            s‚ÇÇ = rand(MC.rng, 1:npole)
        end

        # Try to change position of the s‚ÇÅ pole
        A‚ÇÅ = SE.A[s‚ÇÅ]
        ùïä‚ÇÅ = SE.ùïä[s‚ÇÅ]
        P‚ÇÅ = SE.P[s‚ÇÅ]
        P‚ÇÉ = P‚ÇÅ
        while P‚ÇÉ == P‚ÇÅ || sign(P‚ÇÉ) != sign(ùïä‚ÇÅ)
            P‚ÇÉ = rand(MC.rng, SC.allow)
        end
        P‚ÇÉ = abs(P‚ÇÉ)
        #
        # Try to change position of the s‚ÇÇ pole
        A‚ÇÇ = SE.A[s‚ÇÇ]
        ùïä‚ÇÇ = SE.ùïä[s‚ÇÇ]
        P‚ÇÇ = SE.P[s‚ÇÇ]
        P‚ÇÑ = P‚ÇÇ
        while P‚ÇÑ == P‚ÇÇ || sign(P‚ÇÑ) != sign(ùïä‚ÇÇ)
            P‚ÇÑ = rand(MC.rng, SC.allow)
        end
        P‚ÇÑ = abs(P‚ÇÑ)

        # Calculate change of Green's function
        Œõ‚ÇÅ = view(SC.Œõ, :, P‚ÇÅ)
        Œõ‚ÇÇ = view(SC.Œõ, :, P‚ÇÇ)
        Œõ‚ÇÉ = view(SC.Œõ, :, P‚ÇÉ)
        Œõ‚ÇÑ = view(SC.Œõ, :, P‚ÇÑ)
        @. Œ¥G = ùïä‚ÇÅ * A‚ÇÅ * (Œõ‚ÇÉ - Œõ‚ÇÅ) + ùïä‚ÇÇ * A‚ÇÇ * (Œõ‚ÇÑ - Œõ‚ÇÇ)

        # Calculate new Green's function and goodness-of-fit function
        @. G‚Çô = Œ¥G + SC.G·µß
        œá¬≤ = calc_chi2(G‚Çô, SC.G·µ•)
        Œ¥œá¬≤ = œá¬≤ - SC.œá¬≤

        # Simulated annealing algorithm
        MC.Ptry = MC.Ptry + 1
        if Œ¥œá¬≤ < 0 || min(1.0, exp(-Œ¥œá¬≤ * SC.Œò)) > rand(MC.rng)
            # Update Monte Carlo configuration
            SE.P[s‚ÇÅ] = P‚ÇÉ
            SE.P[s‚ÇÇ] = P‚ÇÑ

            # Update reconstructed Green's function
            @. SC.G·µß = G‚Çô

            # Update goodness-of-fit function
            SC.œá¬≤ = œá¬≤

            # Update Monte Carlo counter
            MC.Pacc = MC.Pacc + 1

            # Save optimal solution
            if SC.œá¬≤ < SC.œá¬≤·µ•[t]
                measure!(t, SE, SC)
            end
        end
    end
end

"""
    try_move_a!(
        t::I,
        MC::StochPXMC{I},
        SE::StochPXElement{I, T},
        SC::StochPXContext{I, T},
        alg::SPX
    ) where {I<:Int, T<:Real}

Change the amplitudes of two randomly selected poles.

### Arguments
* t -> Counter of attempts.
* MC -> A StochPXMC struct.
* SE -> A StochPXElement struct.
* SC -> A StochPXContext struct.
* alg -> A SPX struct.

### Returns
N/A

See also: [`try_move_x`](@ref).
"""
function try_move_a!(t::I,
                     MC::StochPXMC{I},
                     SE::StochPXElement{I,T},
                     SC::StochPXContext{I,T},
                     alg::SPX) where {I<:Int,T<:Real}
    # Get parameters
    ngrid = length(SC.G·µ•)
    npole = alg.npole

    # Sanity check
    if npole == 1
        return
    end

    # It is used to save the change of Green's function
    Œ¥G = zeros(T, ngrid)
    G‚Çô = zeros(T, ngrid)

    # Try to go through each pole
    for _ in 1:npole

        # Select two poles randomly
        # The two poles should not be the same.
        s‚ÇÅ = 1
        s‚ÇÇ = 1
        while s‚ÇÅ == s‚ÇÇ
            s‚ÇÅ = rand(MC.rng, 1:npole)
            s‚ÇÇ = rand(MC.rng, 1:npole)
        end

        # Try to change amplitudes of the two poles, but their sum is kept.
        P‚ÇÅ = SE.P[s‚ÇÅ]
        P‚ÇÇ = SE.P[s‚ÇÇ]
        A‚ÇÅ = SE.A[s‚ÇÅ]
        A‚ÇÇ = SE.A[s‚ÇÇ]
        A‚ÇÉ = T(0)
        A‚ÇÑ = T(0)
        ùïä‚ÇÅ = SE.ùïä[s‚ÇÅ]
        ùïä‚ÇÇ = SE.ùïä[s‚ÇÇ]

        if ùïä‚ÇÅ == ùïä‚ÇÇ
            while true
                Œ¥A = rand(MC.rng) * (A‚ÇÅ + A‚ÇÇ) - A‚ÇÅ
                A‚ÇÉ = A‚ÇÅ + Œ¥A
                A‚ÇÑ = A‚ÇÇ - Œ¥A

                if 1.0 > A‚ÇÉ > 0.0 && 1.0 > A‚ÇÑ > 0.0
                    break
                end
            end
        else
            while true
                _Œ¥A = rand(MC.rng) * (A‚ÇÅ + A‚ÇÇ) - A‚ÇÅ
                Œ¥A = rand(MC.rng) > 0.5 ? _Œ¥A * (+T(1)) : _Œ¥A * (-T(1))
                A‚ÇÉ = (ùïä‚ÇÅ * A‚ÇÅ + Œ¥A) / ùïä‚ÇÅ
                A‚ÇÑ = (ùïä‚ÇÇ * A‚ÇÇ - Œ¥A) / ùïä‚ÇÇ

                if 1.0 > A‚ÇÉ > 0.0 && 1.0 > A‚ÇÑ > 0.0
                    break
                end
            end
        end

        # Calculate change of Green's function
        Œõ‚ÇÅ = view(SC.Œõ, :, P‚ÇÅ)
        Œõ‚ÇÇ = view(SC.Œõ, :, P‚ÇÇ)
        @. Œ¥G = ùïä‚ÇÅ * (A‚ÇÉ - A‚ÇÅ) * Œõ‚ÇÅ + ùïä‚ÇÇ * (A‚ÇÑ - A‚ÇÇ) * Œõ‚ÇÇ

        # Calculate new Green's function and goodness-of-fit function
        @. G‚Çô = Œ¥G + SC.G·µß
        œá¬≤ = calc_chi2(G‚Çô, SC.G·µ•)
        Œ¥œá¬≤ = œá¬≤ - SC.œá¬≤

        # Simulated annealing algorithm
        MC.Atry = MC.Atry + 1
        if Œ¥œá¬≤ < 0 || min(1.0, exp(-Œ¥œá¬≤ * SC.Œò)) > rand(MC.rng)
            # Update Monte Carlo configuration
            SE.A[s‚ÇÅ] = A‚ÇÉ
            SE.A[s‚ÇÇ] = A‚ÇÑ

            # Update reconstructed Green's function
            @. SC.G·µß = G‚Çô

            # Update goodness-of-fit function
            SC.œá¬≤ = œá¬≤

            # Update Monte Carlo counter
            MC.Aacc = MC.Aacc + 1

            # Save optimal solution
            if SC.œá¬≤ < SC.œá¬≤·µ•[t]
                measure!(t, SE, SC)
            end
        end
    end
end

"""
    try_move_x!(
        t::I,
        MC::StochPXMC{I},
        SE::StochPXElement{I, T},
        SC::StochPXContext{I, T},
        alg::SPX
    ) where {I<:Int, T<:Real}

Exchange the amplitudes of two randomly selected poles.

### Arguments
* t -> Counter of attempts.
* MC -> A StochPXMC struct.
* SE -> A StochPXElement struct.
* SC -> A StochPXContext struct.
* alg -> A SPX struct.

### Returns
N/A

See also: [`try_move_a`](@ref).
"""
function try_move_x!(t::I,
                     MC::StochPXMC{I},
                     SE::StochPXElement{I,T},
                     SC::StochPXContext{I,T},
                     alg::SPX) where {I<:Int,T<:Real}
    # Get parameters
    ngrid = length(SC.G·µ•)
    npole = alg.npole

    if npole == 1
        return
    end

    # It is used to save the change of Green's function
    Œ¥G = zeros(T, ngrid)
    G‚Çô = zeros(T, ngrid)

    # Try to go through each pole
    for _ in 1:npole

        # Select two poles randomly
        # The positions of the two poles are different,
        # but their signs should be the same.
        s‚ÇÅ = 1
        s‚ÇÇ = 1
        while (s‚ÇÅ == s‚ÇÇ) || (SE.ùïä[s‚ÇÅ] != SE.ùïä[s‚ÇÇ])
            s‚ÇÅ = rand(MC.rng, 1:npole)
            s‚ÇÇ = rand(MC.rng, 1:npole)
        end

        # Try to swap amplitudes of the two poles, but their sum is kept.
        P‚ÇÅ = SE.P[s‚ÇÅ]
        P‚ÇÇ = SE.P[s‚ÇÇ]
        A‚ÇÅ = SE.A[s‚ÇÅ]
        A‚ÇÇ = SE.A[s‚ÇÇ]
        A‚ÇÉ = A‚ÇÇ
        A‚ÇÑ = A‚ÇÅ
        ùïä‚ÇÅ = SE.ùïä[s‚ÇÅ]
        ùïä‚ÇÇ = SE.ùïä[s‚ÇÇ]

        # Calculate change of Green's function
        Œõ‚ÇÅ = view(SC.Œõ, :, P‚ÇÅ)
        Œõ‚ÇÇ = view(SC.Œõ, :, P‚ÇÇ)
        @. Œ¥G = ùïä‚ÇÅ * (A‚ÇÉ - A‚ÇÅ) * Œõ‚ÇÅ + ùïä‚ÇÇ * (A‚ÇÑ - A‚ÇÇ) * Œõ‚ÇÇ

        # Calculate new Green's function and goodness-of-fit function
        @. G‚Çô = Œ¥G + SC.G·µß
        œá¬≤ = calc_chi2(G‚Çô, SC.G·µ•)
        Œ¥œá¬≤ = œá¬≤ - SC.œá¬≤

        # Simulated annealing algorithm
        MC.Xtry = MC.Xtry + 1
        if Œ¥œá¬≤ < 0 || min(1.0, exp(-Œ¥œá¬≤ * SC.Œò)) > rand(MC.rng)
            # Update Monte Carlo configuration
            SE.A[s‚ÇÅ] = A‚ÇÉ
            SE.A[s‚ÇÇ] = A‚ÇÑ

            # Update reconstructed Green's function
            @. SC.G·µß = G‚Çô

            # Update goodness-of-fit function
            SC.œá¬≤ = œá¬≤

            # Update Monte Carlo counter
            MC.Xacc = MC.Xacc + 1

            # Save optimal solution
            if SC.œá¬≤ < SC.œá¬≤·µ•[t]
                measure!(t, SE, SC)
            end
        end
    end
end
